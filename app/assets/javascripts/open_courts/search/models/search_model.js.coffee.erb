class OpenCourts.SearchModel extends Backbone.AbstractModel
  @include Util.Logger
  @include Util.Model.Attributes

  defaults:
    page:  [1]

  initialize: ->
    @.log 'Initializing ...'

    @page = 1

    @.log 'Initialized.'

  getPage: ->
    @.getValue('page') or 1

  setPage: (value) ->
    value = 1 if !value or isNaN(value)

    @.setValue('page', value)

  query: ->
    type: @type, data: @.toJSON()

  search: (callback) ->
    $.ajax
      url: "/search"
      type: "GET"
      dataType: 'json'
      data:
        @.query()
      success: (response) =>
        @facets = response.facets

        @.log "Response: #{@.inspect response}"

        callback?(response)
      error: (error) =>
        @.err "Error occured while searching (model=#{@.toJSON()})"

        # dirty fix for server errors
        document.write(error.responseText)

  values: (field) ->
    @facets?[field]?.map (facet) -> facet.value

  filterFacets: (field, value) ->
    (@facets?[field].filter (e) -> e.value == value)?[0]

  facet: (field, value) ->
    @.filterFacets(field, value)?.count

  label: (field, value) ->
    @.filterFacets(field, value)?.alias
